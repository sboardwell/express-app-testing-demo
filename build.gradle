plugins {
  id "com.github.node-gradle.node" version "3.0.1"
}

node {
    download = false
    npmInstallCommand = "ci"
}

npmInstall {
    args = ["--prefer-offline", "--no-audit"]
    outputs.cacheIf { true }
}

task npmLint(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'ci:lint']

    outputs.cacheIf { true }

    inputs.dir(file("app"))
        .withPropertyName("appFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.files("package.json", "package-lock.json", ".eslintrc")
        .withPropertyName("configFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.file("eslint.xml")
        .withPropertyName("eslintXml")

}

task npmLintNoDep(type: NpmTask) {
    args = ['run', 'ci:lint-no-dep']

    outputs.cacheIf { true }

    inputs.dir(file("app"))
        .withPropertyName("appFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.files("package.json", "package-lock.json", ".eslintrc")
        .withPropertyName("configFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.file("eslint.xml")
        .withPropertyName("eslintXml")

}

task npmCoverage(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'ci:coverage']

    outputs.cacheIf { true }

    inputs.dir(file("app"))
        .withPropertyName("appFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.files("package.json", "package-lock.json")
        .withPropertyName("configFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.dir("coverage")
        .withPropertyName("coverageFiles")
}

task npmTest(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'ci:test']

    outputs.cacheIf { true }

    inputs.dir(file("app"))
        .withPropertyName("appFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir(file("e2e_tests"))
        .withPropertyName("e2eFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.files("package.json", "package-lock.json")
        .withPropertyName("configFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.file("${project.rootDir}/junit.xml")
        .withPropertyName("junitXml")
}
